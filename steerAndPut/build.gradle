plugins {
    id "com.github.hierynomus.license" version "0.15.0"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.jaredsburrows.license"

preBuild.doFirst {
    if (file("google-services.json").exists()) {
        apply plugin: 'com.google.gms.google-services'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "se.geecity.android.steerandput"
        minSdkVersion 16
        targetSdkVersion 28
        manifestPlaceholders = [mapsApiKey: MAPS_API_KEY]
        multiDexEnabled true
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }

        buildTypes.each {
            it.buildConfigField 'String', 'BICYCLESERVICE_API_KEY', /"${this.properties['BICYCLESERVICE_API_KEY']}"/
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

license {
    header rootProject.file('mit.header')
    strictCheck true
    mapping('java', 'SLASHSTAR_STYLE')
    mapping('kt', 'SLASHSTAR_STYLE')
    ext.year = '2019'
    ext.name = 'Alexander Widar'
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug') {
        task.dependsOn 'licenseFormat'
    }
}

task licenseFormatForKotlin(type: com.hierynomus.gradle.license.tasks.LicenseFormat) {
    source = fileTree(dir: "src/main/java").include("**/*.kt")
}
licenseFormat.dependsOn licenseFormatForKotlin

dependencies {

    implementation project(":domain")
    implementation project(":data")

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'com.google.android.material:material:1.1.0-alpha07'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'org.koin:koin-android-viewmodel:2.0.0-GA2'
    implementation 'com.google.firebase:firebase-core:17.0.0'
    implementation 'com.google.firebase:firebase-inappmessaging-display:18.0.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.2.0"

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.mockito:mockito-core:2.21.0'
    testImplementation "com.google.truth:truth:0.44"
    testImplementation 'org.robolectric:robolectric:4.1'
}
repositories {
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}
